apply plugin: 'java'
apply plugin: 'maven'

group = 'tk.racket.utils'
version = '0.0.1'

description = """Http Client for Java"""

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

dependencies {
    testCompile 'junit:junit:4.12'
}

repositories {
    mavenCentral()
}

//
// Publishment
//
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task testJar(type: Jar, dependsOn: test) {
    classifier = 'tests'
    from sourceSets.test.output
}

task testSourcesJar(type: Jar, dependsOn: test) {
    classifier = 'test-sources'
    from sourceSets.test.allSource
}

artifacts {
    archives sourcesJar, javadocJar, testJar, testSourcesJar
}

uploadArchives {
    def nexusUser = System.getenv('nexusUsername')
    def nexusPassword = System.getenv('nexusPassword')
    def nexusReleaseUrl = 'https://nexus.dmrail.games/repository/maven-releases/'

    repositories {
        mavenDeployer {
            repository(url: nexusReleaseUrl) {
                authentication(userName: nexusUser, password: nexusPassword);
            }
        }
    }
}


//
// Tests
//
test {
    useJUnit()

    maxHeapSize = '1G'
    
    testLogging {
        showStandardStreams = true
    }
}
